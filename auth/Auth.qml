import QtQuick
import QtQuick.Controls
import QtQuick.Layouts 1.15
import Qt5Compat.GraphicalEffects
import QtQml 2.15
import "../common" as Common

Window {
    id: authWindow
    width: 420
    height: 500
    visible: true
    title: "–í—Ö–æ–¥ –≤ —Å–∏—Å—Ç–µ–º—É | " + appName
    color: "transparent"
    flags: Qt.Window | Qt.FramelessWindowHint
    minimumHeight: 500
    minimumWidth: 420

    property bool isWindowMaximized: false
    property int baseNormalWidth: 420
    property int baseNormalHeight: 500
    property int localServerExtraHeight: 100
    property int registrationExtraHeight: 150

    property string _errorMessage: ""
    property bool _showingError: false
    property string _successMessage: ""
    property bool _showingSuccess: false
    property bool _isLoading: false

    property string remoteApiBaseUrl: "http://deltablast.fun"
    property int remotePort: 5000

    property int _minLoadingTime: 500
    property var _loginResult: null

    property bool _showingRegistration: false
    property bool _isGoodClosing: false
    property string authToken: ""

    // –§–∏–∫—Å–∏—Ä–æ–≤–∞–Ω–Ω—ã–µ –ø—Ä–∏—Ä–∞—â–µ–Ω–∏—è –¥–ª—è –º–∞—Å—à—Ç–∞–±–∏—Ä–æ–≤–∞–Ω–∏—è
    property int widthIncrement: 90
    property int heightIncrement: 120

    // –ó–∞—â–∏—Ç–∞ –æ—Ç —Ä–µ–∫—É—Ä—Å–∏–∏
    property bool _updatingWindowHeight: false

    // –û–ø—Ä–µ–¥–µ–ª–µ–Ω–∏–µ –º–æ–±–∏–ª—å–Ω–æ–≥–æ —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞ –ø–æ –û–°
    property bool isMobile: Qt.platform.os === "android" || Qt.platform.os === "ios" ||
                           Qt.platform.os === "tvos" || Qt.platform.os === "wasm"
    property bool disableResize: isMobile

    // –û—Ç—Å—Ç—É–ø—ã –¥–ª—è Android —Å–∏—Å—Ç–µ–º–Ω—ã—Ö –∫–Ω–æ–ø–æ–∫
    property int androidTopMargin: (Qt.platform.os === "android") ? 16 : 0
    property int androidBottomMargin: (Qt.platform.os === "android" &&  parent.width < parent.height) ? 28 : 0

    signal loginSuccessful(string token, var userData)

    // Behavior –∞–Ω–∏–º–∞—Ü–∏–∏ –¥–ª—è android & linux & ios
    Behavior on height {
        enabled: Qt.platform.os !== "windows"
        NumberAnimation {
            duration: 250
            easing.type: Easing.Linear
        }
    }

    AuthAPI {
        id: authAPI
        remoteApiBaseUrl: authWindow.remoteApiBaseUrl
        remotePort: authWindow.remotePort
    }

    Loader {
        id: mainWindowLoader
        active: false
        asynchronous: true
        source: "../main/main/Main.qml"
        onLoaded: if (item) item.show()
    }

    Timer {
        id: tokenCheckTimer
        interval: 100
        onTriggered: {
            var savedToken = settingsManager.authToken || "";
            checkSavedToken(savedToken);
        }
    }

    Timer {
        id: safeUpdateHeightTimer
        interval: 16
        onTriggered: updateWindowHeight()
    }

    Component.onCompleted: {
        // –ù–∞ –º–æ–±–∏–ª—å–Ω—ã—Ö —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞—Ö —É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º —Ñ–∏–∫—Å–∏—Ä–æ–≤–∞–Ω–Ω—ã–π —Ä–∞–∑–º–µ—Ä
        if (disableResize) {
            authWindow.width = Math.min(Screen.width, baseNormalWidth);
            authWindow.height = Math.min(Screen.height, baseNormalHeight);
            authWindow.minimumWidth = authWindow.width;
            authWindow.maximumWidth = authWindow.width;
            authWindow.minimumHeight = authWindow.height;
            authWindow.maximumHeight = authWindow.height;
        }

        serverConfig.updateFromSettings();
        updateWindowHeight();
        windowContainer.forceActiveFocus();

        // –ò–Ω–∏—Ü–∏–∞–ª–∏–∑–∏—Ä—É–µ–º AuthAPI —Å —Ç–µ–∫—É—â–∏–º–∏ –Ω–∞—Å—Ç—Ä–æ–π–∫–∞–º–∏
        updateAuthAPI();

        // –ü—Ä–æ–≤–µ—Ä–∫–∞ —Å–µ—Ä–≤–µ—Ä–∞ —á–µ—Ä–µ–∑ AuthAPI
        authAPI.testConnection(function(success) {
            console.log("üß™ –°—Ç–∞—Ç—É—Å —Å–µ—Ä–≤–µ—Ä–∞:", success ? "–¥–æ—Å—Ç—É–ø–µ–Ω" : "–Ω–µ–¥–æ—Å—Ç—É–ø–µ–Ω");

            var savedToken = settingsManager.authToken || "";
            if (savedToken && savedToken.length > 0) {
                console.log("üîê –ù–∞–π–¥–µ–Ω —Å–æ—Ö—Ä–∞–Ω–µ–Ω–Ω—ã–π —Ç–æ–∫–µ–Ω, –ø—Ä–æ–≤–µ—Ä—è–µ–º...");
                tokenCheckTimer.start();
            }
        });
    }

    // –û–±—Ä–∞–±–æ—Ç—á–∏–∫ –∫–Ω–æ–ø–∫–∏ "–ù–∞–∑–∞–¥" –¥–ª—è Android –∏ –î–µ—Å–∫—Ç–æ–ø–∞
    onClosing: (close) => {
        if (isMobile) {
            close.accepted = false
            handleBackButton()
        } else if (!_isGoodClosing) {
            Qt.quit()
        }
    }


    // –ö–æ–º–ø–æ–Ω–µ–Ω—Ç –¥–ª—è –¥–∏–∞–ª–æ–≥–∞ –≤—ã—Ö–æ–¥–∞
    Component {
        id: exitDialogComponent
        Common.MobileCloseApp {
            onConfirmed: Qt.exit(0)
            onCancelled: close()
            Component.onCompleted: openDialog()
        }
    }

    function toggleMaximize() {
        // –ù–∞ –º–æ–±–∏–ª—å–Ω—ã—Ö —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞—Ö –ø–æ–ª–Ω–æ—Å—Ç—å—é –æ—Ç–∫–ª—é—á–∞–µ–º –º–∞—Å—à—Ç–∞–±–∏—Ä–æ–≤–∞–Ω–∏–µ
        if (disableResize) {
            return;
        }

        if (isWindowMaximized) {
            // –í–æ–∑–≤—Ä–∞—â–∞–µ–º –∫ –∏—Å—Ö–æ–¥–Ω–æ–º—É —Ä–∞–∑–º–µ—Ä—É —Å –∞–Ω–∏–º–∞—Ü–∏–µ–π
            authWindow.width = baseNormalWidth;
            authWindow.height = calculateBaseHeight();
            isWindowMaximized = false;
        } else {
            // –£–≤–µ–ª–∏—á–∏–≤–∞–µ–º –Ω–∞ —Ñ–∏–∫—Å–∏—Ä–æ–≤–∞–Ω–Ω—ã–µ –∑–Ω–∞—á–µ–Ω–∏—è —Å –∞–Ω–∏–º–∞—Ü–∏–µ–π
            var targetWidth = Math.min(baseNormalWidth + widthIncrement, maximumWidth);
            var baseHeightValue = calculateBaseHeight();
            var targetHeight = Math.min(baseHeightValue + heightIncrement, maximumHeight);

            authWindow.width = targetWidth;
            authWindow.height = targetHeight;
            isWindowMaximized = true;
        }
    }

    // –§—É–Ω–∫—Ü–∏—è –¥–ª—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –≤—ã—Å–æ—Ç—ã –æ–∫–Ω–∞ —Å –∞–Ω–∏–º–∞—Ü–∏–µ–π
    function updateWindowHeight() {
        // –ù–∞ –º–æ–±–∏–ª—å–Ω—ã—Ö —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞—Ö –∏–≥–Ω–æ—Ä–∏—Ä—É–µ–º –∏–∑–º–µ–Ω–µ–Ω–∏–µ –≤—ã—Å–æ—Ç—ã
        if (disableResize) return;
        if (_updatingWindowHeight) return;

        _updatingWindowHeight = true;

        var targetHeight = calculateBaseHeight();

        if (!isWindowMaximized) {
            if (Math.abs(authWindow.height - targetHeight) > 1) {
                authWindow.height = targetHeight;
            }
        } else {
            var baseHeightValue = calculateBaseHeight();
            var maximizedHeight = Math.min(maximumHeight, baseHeightValue + heightIncrement);
            if (Math.abs(authWindow.height - maximizedHeight) > 1) {
                authWindow.height = maximizedHeight;
            }
        }

        _updatingWindowHeight = false;
    }

    // –†–∞—Å—á–µ—Ç –±–∞–∑–æ–≤–æ–π –≤—ã—Å–æ—Ç—ã
    function calculateBaseHeight() {
        var targetHeight = baseNormalHeight;

        if (settingsManager.useLocalServer && !_showingRegistration) {
            targetHeight += localServerExtraHeight;
        }

        if (_showingError) {
            targetHeight += 60;
        }

        if (_showingRegistration) {
            targetHeight += registrationExtraHeight;
        }

        return Math.max(minimumHeight, Math.min(maximumHeight, targetHeight));
    }

    function saveServerConfig(serverAddress) {
        try {
            settingsManager.serverAddress = serverAddress;
            serverConfig.updateFromSettings();
            updateAuthAPI(); // –î–æ–±–∞–≤–ª—è–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ API
            showSuccess("–ù–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–µ—Ä–≤–µ—Ä–∞ —É—Å–ø–µ—à–Ω–æ —Å–æ—Ö—Ä–∞–Ω–µ–Ω—ã.");
        } catch (error) {
            showError("–û—à–∏–±–∫–∞ —Å–æ—Ö—Ä–∞–Ω–µ–Ω–∏—è –Ω–∞—Å—Ç—Ä–æ–µ–∫");
        }
    }

    function resetSettings() {
        try {
            settingsManager.serverAddress = "http://localhost:5000";
            settingsManager.useLocalServer = true; // –°–±—Ä–∞—Å—ã–≤–∞–µ–º —Ç–∏–ø —Å–µ—Ä–≤–µ—Ä–∞
            serverConfig.updateFromSettings();
            updateAuthAPI(); // –î–æ–±–∞–≤–ª—è–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ API
            showSuccess("–ù–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–±—Ä–æ—à–µ–Ω—ã –∫ –∑–Ω–∞—á–µ–Ω–∏—è–º –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é.");
        } catch (error) {
            showError("–û—à–∏–±–∫–∞ —Å–±—Ä–æ—Å–∞ –Ω–∞—Å—Ç—Ä–æ–µ–∫: " + error);
        }
    }

    function checkSavedToken(token) {
        if (_isLoading) return;

        _isLoading = true;
        showLoading();

        authAPI.authToken = token;

        authAPI.validateToken(function(result) {
            _isLoading = false;
            hideLoading();

            if (result.success && result.valid) {
                _isGoodClosing = true;
                authToken = token;
                settingsManager.authToken = token;
                mainWindowLoader.active = true;
                authWindow.close();
            } else {
                settingsManager.authToken = "";
                authToken = "";
                showError("–í–∞—à–∞ —Å–µ—Å—Å–∏—è –∏—Å—Ç–µ–∫–ª–∞. –ü–æ–∂–∞–ª—É–π—Å—Ç–∞, –≤–æ–π–¥–∏—Ç–µ —Å–Ω–æ–≤–∞.");
                showLoginForm();
            }
        });
    }

    function showRegistrationForm() {
        _showingRegistration = true;
        safeUpdateHeightTimer.start();
        windowContainer.forceActiveFocus();
    }

    function showLoginForm() {
        _showingRegistration = false;
        safeUpdateHeightTimer.start();
        windowContainer.forceActiveFocus();
    }

    function normalizePhoneNumber(phone) {
        // –ü—Ä–æ–≤–µ—Ä—è–µ–º, —á—Ç–æ phone –Ω–µ undefined –∏–ª–∏ null
        if (!phone) {
            return ""
        }

        // –ü—Ä–µ–æ–±—Ä–∞–∑—É–µ–º –≤ —Å—Ç—Ä–æ–∫—É –Ω–∞ —Å–ª—É—á–∞–π, –µ—Å–ª–∏ –ø–µ—Ä–µ–¥–∞–Ω–æ —á–∏—Å–ª–æ
        var phoneString = String(phone);

        // –£–¥–∞–ª—è–µ–º –≤—Å–µ –Ω–µ—Ü–∏—Ñ—Ä–æ–≤—ã–µ —Å–∏–º–≤–æ–ª—ã
        var digits = phoneString.replace(/\D/g, '')

        // –ï—Å–ª–∏ –Ω–æ–º–µ—Ä –ø—É—Å—Ç–æ–π, –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –ø—É—Å—Ç—É—é —Å—Ç—Ä–æ–∫—É
        if (digits.length === 0) {
            return ""
        }

        // –ï—Å–ª–∏ –Ω–æ–º–µ—Ä –Ω–∞—á–∏–Ω–∞–µ—Ç—Å—è —Å 8, –∑–∞–º–µ–Ω—è–µ–º –Ω–∞ 7
        if (digits.startsWith('8')) {
            digits = '7' + digits.substring(1)
        }
        // –ï—Å–ª–∏ –Ω–æ–º–µ—Ä –Ω–∞—á–∏–Ω–∞–µ—Ç—Å—è –Ω–µ —Å 7 –∏ –Ω–µ —Å 8, –¥–æ–±–∞–≤–ª—è–µ–º 7 –≤ –Ω–∞—á–∞–ª–æ
        else if (!digits.startsWith('7')) {
            digits = '7' + digits
        }

        // –û–≥—Ä–∞–Ω–∏—á–∏–≤–∞–µ–º –¥–ª–∏–Ω—É –¥–æ 11 —Ü–∏—Ñ—Ä
        digits = digits.substring(0, 11)

        // –ï—Å–ª–∏ –æ—Å—Ç–∞–ª–∞—Å—å —Ç–æ–ª—å–∫–æ –æ–¥–Ω–∞ —Ü–∏—Ñ—Ä–∞ 7, –≤–æ–∑–≤—Ä–∞—â–∞–µ–º –ø—É—Å—Ç—É—é —Å—Ç—Ä–æ–∫—É
        if (digits === '7') {
            return ""
        }

        return digits
    }

    function attemptRegistration() {
        if (!isRegistrationFormValid() || _isLoading) return;

        _isLoading = true;
        showLoading();

        try {
            var nameData = registrationForm.parseFullName();
            var userData = {
                username: registrationForm.usernameField.text,
                email: registrationForm.emailField.text,
                password: registrationForm.passwordField.text,
                firstName: nameData.firstName,
                lastName: nameData.lastName,
                middleName: nameData.middleName,
                phoneNumber: normalizePhoneNumber(registrationForm.phoneField.text)
            };

            authAPI.sendRegistrationRequest(userData, function(result) {
                registrationResultTimer.result = result;
                registrationResultTimer.start();
            });
        } catch (error) {
            hideLoading();
            _isLoading = false;
            showError("–û—à–∏–±–∫–∞ –ø—Ä–∏ —Ä–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏–∏: " + error);
        }
    }

    Timer {
        id: registrationResultTimer
        property var result
        onTriggered: {
            hideLoading();
            _isLoading = false;
            if (result.success) {
                var registeredEmail = registrationForm.emailField.text;
                showLoginForm();
                setLoginEmail(registeredEmail);
                registrationForm.clearAllFields();
                showSuccess("–†–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏—è –ø—Ä–æ—à–ª–∞ —É—Å–ø–µ—à–Ω–æ! –¢–µ–ø–µ—Ä—å –≤—ã –º–æ–∂–µ—Ç–µ –≤–æ–π—Ç–∏ –≤ —Å–∏—Å—Ç–µ–º—É.")
            } else {
                showError(result.message || result.error);
            }
        }
    }

    function setLoginEmail(email) {
        if (loginForm && loginForm.loginField && email) {
            loginForm.loginField.text = email;
        }
    }

    function isRegistrationFormValid() {
        return registrationForm.usernameField.text.length > 0 &&
               registrationForm.hasValidFullName &&
               registrationForm.emailField.text.length > 0 &&
               registrationForm.passwordField.text.length > 0 &&
               registrationForm.confirmPasswordField.text.length > 0 &&
               registrationForm.passwordField.text === registrationForm.confirmPasswordField.text;
    }

    function attemptLogin() {
        if (!isFormValid() || _isLoading) return;

        var login = loginForm.loginField.text.trim();
        var password = loginForm.passwordField.text;

        if (login === "" || password === "") {
            showError("–õ–æ–≥–∏–Ω –∏ –ø–∞—Ä–æ–ª—å –Ω–µ –º–æ–≥—É—Ç –±—ã—Ç—å –ø—É—Å—Ç—ã–º–∏");
            return;
        }

        showLoading();

        try {
            authAPI.sendLoginRequest(login, password, function(result) {
                _isLoading = true;
                _loginResult = result;
                loadingTimer.start();

                if (result.success && result.token) {
                    authToken = result.token;
                    settingsManager.authToken = result.token;
                    if (mainWindowLoader.item) {
                        mainWindowLoader.item.initializeProfile(result.token, authAPI.baseUrl);
                    }
                }
            });
        } catch (error) {
            hideLoading();
            _isLoading = false;
            showError("–û—à–∏–±–∫–∞ –ø—Ä–∏ –≤—Ö–æ–¥–µ: " + error);
        }
    }

    function updateAuthAPI() {
        var baseUrl = settingsManager.useLocalServer ?
            settingsManager.serverAddress :
            (remoteApiBaseUrl + ":" + remotePort);

        authAPI.initialize(authAPI.authToken, baseUrl);
    }

    function showError(message) {
        _successMessage = "";
        _showingSuccess = false;
        _errorMessage = message;
        _showingError = message !== "";

        if (_showingError) {
            errorAutoHideTimer.restart();
            safeUpdateHeightTimer.start();
        } else {
            safeUpdateHeightTimer.start();
        }
    }

    function showSuccess(message) {
        _errorMessage = "";
        _showingError = false;
        _successMessage = message;
        _showingSuccess = message !== "";

        if (_showingSuccess) {
            successAutoHideTimer.restart();
        }
    }

    function showLoading() {
        loadingAnimation.visible = true;
        loadingAnimation.opacity = 1;

        if (_showingRegistration) {
            registrationForm.opacity = 0.6;
            registrationForm.registerButton.enabled = false;
        } else {
            loginForm.opacity = 0.6;
            loginForm.loginButton.enabled = false;
        }
    }

    function hideLoading() {
        loadingAnimation.opacity = 0;
        loadingAnimation.visible = false;

        if (_showingRegistration) {
            registrationForm.opacity = 1.0;
            registrationForm.registerButton.enabled = true;
        } else {
            loginForm.opacity = 1.0;
            loginForm.loginButton.enabled = true;
        }
    }

    function isFormValid() {
        return loginForm.loginField.text.length > 0 && loginForm.passwordField.text.length > 0;
    }

    Rectangle {
        id: windowContainer
        anchors.fill: parent
        radius: 24
        color: "#f0f0f0"
        clip: true
        focus: true

        Keys.onPressed: (event) => {
            if ((event.key === Qt.Key_Return || event.key === Qt.Key_Enter) && (event.modifiers & Qt.ControlModifier)) {
                if (_showingRegistration) {
                    attemptRegistration();
                } else {
                    attemptLogin();
                }
                event.accepted = true;
            }
        }

        Rectangle {
            id: background
            anchors.fill: parent
            gradient: Gradient {
                GradientStop { position: 0.0; color: "#6a11cb" }
                GradientStop { position: 1.0; color: "#2575fc" }
            }
            z: 0
            radius: 20
        }

        Common.PolygonBackground {
            anchors.fill: parent
            polygonCount: 4
            isMobile: authWindow.isMobile
        }

        // –ò—Å–∫—Ä—ã –Ω–∞ —Ñ–æ–Ω–µ
        Common.SparksBackground {
            anchors.fill: parent
            isMobile: authWindow.isMobile
            z: 1
            enabled: !authWindow._isLoading
        }

        Common.BottomBlur {
            anchors {
                left: parent.left
                right: parent.right
                bottom: parent.bottom
            }
            blurHeight: androidBottomMargin
            blurOpacity: 0.8
            z: 2
            isMobile: authWindow.isMobile
        }

        Common.TitleBar {
            id: titleBar
            anchors {
                top: parent.top
                left: parent.left
                right: parent.right
                topMargin: 10 + authWindow.androidTopMargin
                leftMargin: 10
                rightMargin: 10
            }
            isWindowMaximized: authWindow.isWindowMaximized
            currentView: _showingRegistration ? "–†–µ–≥–∏—Å—Ç—Ä–∞—Ü–∏—è" : "–í—Ö–æ–¥ –≤ —Å–∏—Å—Ç–µ–º—É"
            window: authWindow
            isMobile: authWindow.isMobile
            authmenu: true

            onToggleMaximize: authWindow.toggleMaximize()
            onShowMinimized: authWindow.showMinimized()
            onClose: Qt.quit()
        }

        Message {
            id: errorMessage
            anchors {
                horizontalCenter: parent.horizontalCenter
                top: titleBar.bottom
                topMargin: 8
            }
            width: parent.width * 0.78
            scale: isMobile ? 1.12 : 1
            messageText: _errorMessage
            showingMessage: _showingError
            messageType: "error"

            onCloseMessage: showError("")
        }

        Message {
            id: successMessage
            anchors {
                horizontalCenter: parent.horizontalCenter
                top: errorMessage.bottom
                topMargin: 8
            }
            width: parent.width * 0.78
            scale: isMobile ? 1.12 : 1
            messageText: _successMessage
            showingMessage: _showingSuccess
            messageType: "success"

            onCloseMessage: showSuccess("")
        }

        ServerConfig {
            id: serverConfig
            anchors {
                horizontalCenter: parent.horizontalCenter
                top: successMessage.bottom
                topMargin: 24
            }
            width: parent.width * 0.78
            scale: isMobile ? 1.12 : 1
            visible: !_showingRegistration
            isMobile: authWindow.isMobile

            onServerTypeToggled: function(useLocal) {
                settingsManager.useLocalServer = useLocal;
                safeUpdateHeightTimer.start();
                updateAuthAPI();
            }

            onSaveServerConfig: function(serverAddress) {
                authWindow.saveServerConfig(serverAddress);
            }

            onResetSettings: {
                authWindow.resetSettings();
            }
        }

        RegistrationForm {
            id: registrationForm
            anchors {
                horizontalCenter: parent.horizontalCenter
                top: successMessage.bottom
                topMargin: 24
            }
            width: parent.width * 0.78
            scale: isMobile ? 1.12 : 1
            visible: _showingRegistration
            isMobile: authWindow.isMobile

            onAttemptRegistration: authWindow.attemptRegistration()
            onShowLoginForm: authWindow.showLoginForm()
        }

        LoginForm {
            id: loginForm
            anchors {
                horizontalCenter: parent.horizontalCenter
                top: serverConfig.visible ? serverConfig.bottom : successMessage.bottom
                topMargin: isMobile ? 40 : 32
            }
            width: parent.width * 0.78
            scale: isMobile ? 1.12 : 1
            visible: !_showingRegistration
            isMobile: authWindow.isMobile

            onAttemptLogin: authWindow.attemptLogin()
        }

        LoadingAnimation {
            id: loadingAnimation
            scale: isMobile ? 1.12 : 1
            anchors.centerIn: parent
        }

        CopyrightFooter {
            scale: isMobile ? 1.12 : 1
            anchors {
                horizontalCenter: parent.horizontalCenter
                bottom: parent.bottom
                bottomMargin: 20 + androidBottomMargin
            }
            isMobile: authWindow.isMobile
        }

        Timer {
            id: loadingTimer
            interval: 1
            onTriggered: {
                hideLoading();
                _isLoading = false;
                if (_loginResult) {
                    if (_loginResult.success) {
                        _isGoodClosing = true;
                        showSuccess(_loginResult.message || "–í—Ö–æ–¥ –≤—ã–ø–æ–ª–Ω–µ–Ω —É—Å–ø–µ—à–Ω–æ");
                        mainWindowLoader.active = true;
                        authWindow.close();
                    } else {
                        var errorMessage = _loginResult.error || _loginResult.message || "–û—à–∏–±–∫–∞ –≤—Ö–æ–¥–∞";
                        showError(errorMessage);
                    }
                    _loginResult = null;
                }
            }
        }

        Timer {
            id: errorAutoHideTimer
            interval: 7500
            onTriggered: showError("")
        }

        Timer {
            id: successAutoHideTimer
            interval: 5000
            onTriggered: showSuccess("")
        }
    }

    Connections {
        target: settingsManager
        function onUseLocalServerChanged() {
            safeUpdateHeightTimer.start();
            updateAuthAPI();
        }
    }

    // –í—Å–ø–æ–º–æ–≥–∞—Ç–µ–ª—å–Ω—ã–µ —Ñ—É–Ω–∫—Ü–∏–∏
    function handleBackButton() {
        if (_showingRegistration) {
            showLoginForm();
        } else {
            showExitDialog();
        }
    }

    function showExitDialog() {
        // –°–Ω–∏–º–∞–µ–º —Ñ–æ–∫—É—Å —Å –ª—é–±—ã—Ö –∞–∫—Ç–∏–≤–Ω—ã—Ö —ç–ª–µ–º–µ–Ω—Ç–æ–≤
        windowContainer.forceActiveFocus()
        var dialog = exitDialogComponent.createObject(authWindow);
        dialog.open();
    }
}
